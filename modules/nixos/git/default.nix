{
  lib,
  options,
  config,
  pkgs,
  ...
}:
{
  options.modules.git = {
    enable = lib.mkOption {
      description = "Whether to set up a Git server.";
      default = false;
      type = lib.types.bool;
    };
    repositories = lib.mkOption {
      description = "For each bare repository name, its configuration.";
      default = { };
      type =
        lib.types.submodule {
          options = {
            description = lib.mkOption {
              description = "The description.";
              type = lib.types.uniq lib.types.str;
            };
            owner = lib.mkOption {
              description = "The owner.";
              type = lib.types.uniq lib.types.str;
            };
            baseUrl = lib.mkOption {
              description = "The base URL used to clone the repository through the Git protocol.";
              type = lib.types.uniq lib.types.str;
            };
          };
        }
        |> lib.types.attrsOf;
    };
    stagit = {
      enable = lib.mkOption {
        description = "Whether to enable Stagit.";
        default = false;
        type = lib.types.bool;
      };
      output = lib.mkOption {
        description = "The directory where generated HTML files will reside.";
        type = lib.types.uniq lib.types.path;
      };
      baseUrl = lib.mkOption {
        description = "The base URL used to make links in the Atom feed generated by Stagit.";
        type = lib.types.uniq lib.types.str;
      };
    };
  };

  config = lib.mkIf config.modules.git.enable {
    users = {
      users = {
        git = {
          hashedPassword = "!";
          isSystemUser = true;
          group = "git";
          createHome = true;
          home = "/srv/git";
          shell = "${pkgs.git}/bin/git-shell";
        };
      };
      groups = {
        git = { };
      };
    };

    systemd.services = {
      git-repositories = {
        enable = true;
        wantedBy = [ "multi-user.target" ];
        script =
          config.modules.git.repositories
          |> builtins.mapAttrs (
            name:
            { description, owner, baseUrl }:
            ''
              ${pkgs.git}/bin/git \
                init \
                --quiet \
                --bare \
                --initial-branch master \
                /srv/git/${name}

              ${pkgs.coreutils}/bin/echo "${description}" > /srv/git/${name}/description

              ${pkgs.coreutils}/bin/echo "${owner}" > /srv/git/${name}/owner

              ${pkgs.coreutils}/bin/echo "git://${baseUrl}/${name}" > /srv/git/${name}/url

              ${pkgs.coreutils}/bin/chown --recursive git:git /srv/git/${name}
            ''
          )
          |> builtins.attrValues
          |> builtins.concatStringsSep "\n";
      };
    };

    programs.git = {
      enable = true;
      package = pkgs.git;

      config = {
        init.defaultBranch = "master";
      };
    };

    services.gitDaemon = {
      enable = true;
      package = pkgs.git;

      user = "git";
      group = "git";
      basePath = "/srv/git";
      repositories =
        config.modules.git.repositories
        |> builtins.attrNames
        |> builtins.map (name: "/srv/git/${name}");

      port = 9418;
    };

    systemd.services = {
      stagit =
        let
          inherit (config.modules.git) stagit;
        in
        lib.mkIf stagit.enable {
          enable = true;
          wantedBy = [ "multi-user.target" ];
          script = ''
            ${
              (
                config.modules.git.repositories
                |> builtins.attrNames
                |> builtins.map (name: ''
                  ${pkgs.coreutils}/bin/mkdir -p ${stagit.output}/${name}
                  cd ${stagit.output}/${name}

                  ${pkgs.stagit}/bin/stagit /srv/git/${name}
                '')
              )
              |> builtins.concatStringsSep "\n"
            }

            ${pkgs.stagit}/bin/stagit-index ${
              (
                config.modules.git.repositories
                |> builtins.attrNames
                |> builtins.map (name: "/srv/git/${name}")
                |> builtins.concatStringsSep " "
              )
            } > ${stagit.output}/index.html
          '';
        };
    };

    networking.firewall.allowedTCPPorts = [ 9418 ];
  };
}
